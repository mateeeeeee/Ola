<translation-unit> ::= { import-declaration }* { <function-definition> | <function-declaration> | <global-variable-declaration> | <alias-declaration> }*

<import-declaration> ::= import <import_identifier>;
<import_identifier> ::= <identifier>{.<identifier>}*

<declaration-visibility>  ::= public | private

<function-declaration>    ::= extern <type-specifier> <identifier>( <parameter-list> );
<function-definition>     ::= {<declaration-visibility>}? <type-specifier> <identifier>( <parameter-list> ) <compound-statement>

<global-variable-declaration> ::= extern <type-qualifier> <type-specifier> <identifier>:
                                | {<declaration-visibility>}? <type-qualifier> <type-specifier> <identifier> { = <constant-expression>}?;
								| {<declaration-visibility>}? <type-qualifier> var <identifier> = <constant-expression>;


<parameter-list> ::= <parameter-declaration>
                   | <parameter-list>, <parameter-declaration>
<parameter-declaration> ::= {<type-qualifier>}? <type-specifier> {<identifier>}?

<type-qualifier> ::= {const}?
<type-specifier> ::= void
                   | bool {[]}*
                   | char {[]}*
                   | int  {[]}*
                   | float {[]}*
                   | <enum-identifier> {[]}*
                   | <class-identifier> {[]}*
                   | <alias-identifier> {[]}*

<alias-declaration> ::= alias <alias-identifier> = <type-qualifier><type-specifier>;
	   
<enum-specifier> ::= enum <enum-identifier> { <enumerator-list> }
                   | enum { <enumerator-list> }

<enumerator-list> ::= <enumerator>
                    | <enumerator-list> , <enumerator>

<enumerator> ::= <identifier>
               | <identifier> = <constant-expression>

<enum-identifier> ::= <identifier>
			   
<class-specifier> ::= ... (TODO)

<class-identifier> ::= <identifier>

<constant-expression> ::= <conditional-expression>

<conditional-expression> ::= <logical-or-expression>
                           | <logical-or-expression> ? <expression> : <conditional-expression>

<logical-or-expression> ::= <logical-and-expression>
                          | <logical-or-expression> || <logical-and-expression>

<logical-and-expression> ::= <inclusive-or-expression>
                           | <logical-and-expression> && <inclusive-or-expression>

<inclusive-or-expression> ::= <exclusive-or-expression>
                            | <inclusive-or-expression> | <exclusive-or-expression>

<exclusive-or-expression> ::= <and-expression>
                            | <exclusive-or-expression> ^ <and-expression>

<and-expression> ::= <equality-expression>
                   | <and-expression> & <equality-expression>

<equality-expression> ::= <relational-expression>
                        | <equality-expression> == <relational-expression>
                        | <equality-expression> != <relational-expression>

<relational-expression> ::= <shift-expression>
                          | <relational-expression> < <shift-expression>
                          | <relational-expression> > <shift-expression>
                          | <relational-expression> <= <shift-expression>
                          | <relational-expression> >= <shift-expression>

<shift-expression> ::= <additive-expression>
                     | <shift-expression> << <additive-expression>
                     | <shift-expression> >> <additive-expression>

<additive-expression> ::= <multiplicative-expression>
                        | <additive-expression> + <multiplicative-expression>
                        | <additive-expression> - <multiplicative-expression>

<multiplicative-expression> ::= <unary-expression>
                              | <multiplicative-expression> * <unary-expression>
                              | <multiplicative-expression> / <unary-expression>
                              | <multiplicative-expression> % <unary-expression>
							 

<unary-expression> ::= <postfix-expression>
                     | ++ <unary-expression>
                     | -- <unary-expression>
                     | sizeof (<unary-expression>)
                     | sizeof (<type-specifier>)
                     | length (<unary-expression>)

<postfix-expression> ::= <primary-expression>
                       | <postfix-expression> [ <expression> ]
                       | <postfix-expression> . <identifier>
                       | <postfix-expression> ( {<assignment-expression>}* )
                       | <postfix-expression> ++
                       | <postfix-expression> --

<primary-expression> ::= <identifier>
                       | <constant>
                       | <string>
                       | ( <expression> )

letter	::= {[a-z|A-Z]}
digit	::= [0-9]
identifier ::= letter { letter | digit | _ }*
<string> ::= "{ch}", where ch denotes any printable ASCII character 

<constant> ::= <integer-constant>
             | <character-constant>
             | <floating-constant>
             | <enumeration-constant>
			 
<expression> ::= <assignment-expression>
               | <expression> , <assignment-expression>

<assignment-expression> ::= <conditional-expression>
                          | <unary-expression> <assignment-operator> <assignment-expression>

<assignment-operator> ::= =
                        | *=
                        | /=
                        | %=
                        | +=
                        | -=
                        | <<=
                        | >>=
                        | &=
                        | ^=
                        | |=

<unary-operator> ::= &
                   | *
                   | +
                   | -
                   | ~
                   | !

<local-variable-declaration> ::= <type-qualifier> <type-specifier> <identifier> { = {<initializer> | <init-list-expr> }}?;
                               | <type-qualifier> var <identifier> = {<initializer> | <init-list-expr> };
	
<initializer> ::= <assignment-expression>
<init-list-expr> ::= {<type-specifier>{[<constant-expression>]}+}?{ <initializer>{,<initializer>}* }

<compound-statement> ::= { { <local-variable-declaration> | <statement> | <alias-declaration> }* }

<statement> ::= <labeled-statement>
              | <expression-statement>
              | <compound-statement>
              | <selection-statement>
              | <iteration-statement>
              | <jump-statement>

<labeled-statement> ::= <identifier> : <statement>
                      | case <constant-expression> : <statement>
                      | default : <statement>

<expression-statement> ::= {<expression>}? ;

<selection-statement> ::= if ( <expression> ) <statement>
                        | if ( <expression> ) <statement> else <statement>
                        | switch ( <expression> ) <statement>

<iteration-statement> ::= while ( <expression> ) <statement>
                        | do <statement> while ( <expression> ) ;
                        | for ( {<expression>}? ; {<expression>}? ; {<expression>}? ) <statement>
                        | foreach (<type-qualifier> <type-specifier> <identifier> : <identifier>) <statement>

<jump-statement> ::= goto <identifier> ;
                   | continue ;
                   | break ;
                   | return {<expression>}? ;